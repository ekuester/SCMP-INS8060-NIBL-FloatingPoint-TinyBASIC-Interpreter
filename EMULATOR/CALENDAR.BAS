1 REM ***     CALENDAR PROGRAM     ***
2 REM ***  ONLY FOR NIBL AND NIBLE ***
3 REM AUTEUR:      A. LANGENBERG "BASIC KALENDER",
4 REM TIJDSCHRIFT: ELEKTUUR, NO. 207, JANUARI 1981, PP. 1.63
5 GOTO 800
10 K=-2:L=-1:M=0:PR "YEAR";:INPUT C
12 IF C<1582 PR"I CAN'T REMEMBER AS FAR AS",C:GOTO 10
15 IF C>2100 PR C,"IS TOO FAR INTO THE FUTURE!":GOTO 10
20 PR"                    CALENDAR       ",C:PR""
30 FOR I=1 TO 4
40 K=K+3:L=L+3:M=M+3
50 GOSUB 740
60 A=1:B=K
70 GOSUB 390
80 IF Y=1 O=A :GOTO 100
90 A=A+1: GOTO 70
100 A=30:D=32
110 GOSUB 390
120 IF D=A E=D:GOTO 140
130 A=A+1:GOTO 110
140 A=1:B=L
150 GOSUB 390
160 IF Y=1 P=A:GOTO 180
170 A=A+1:GOTO 150
180 A=29:D=32
190 GOSUB 390
200 IF D=A F=D:GOTO 220
210 A=A+1:GOTO 190
220 A=1:B=M
230 GOSUB 390
240 IF Y=1 Q=A :GOTO 260
250 A=A+1:GOTO 230
260 A=30:D=32
270 GOSUB 390
280 IF D=A G=D:GOTO 300
290 A=A+1:GOTO 270
300 FOR J=0 TO 6
310 GOSUB 660
320 R=O:S=8-O:D=E:GOSUB 560
330 O=O+1:R=P:S=8-P:D=F:GOSUB 560
340 P=P+1:R=Q:S=8-Q:D=G:GOSUB 560
350 Q=Q+1:PR"":NEXT J
355 PR""
360 NEXT I
370 END
380 REM SUBR. CALCULATE DAY
390 IF B=2 IF A>29 GOTO 500
400 IF B=2 IF A=29 IF MOD(C,4)>0 GOTO 500
410 IF B=2 IF A=29 IF C=1700 GOTO 500
420 IF B=2 IF A=29 IF C=1800 GOTO 500
430 IF B=2 IF A=29 IF C=1900 GOTO 500
440 IF A=31 IF B=4 GOTO 500
450 IF A=31 IF B=6 GOTO 500
460 IF A=31 IF B=9 GOTO 500
470 IF A=31 IF B=11 GOTO 500
480 IF A>0 IF A<32 IF B>0 IF B<13 GOTO 510
500 D=A:RETURN
510 IF B>2 GOTO 530
520 X=C+A+31*(B-1)+(C-1)/4-3*((C-1)/100+1)/4:GOTO 540
530 X=C+A+31*(B-1)-(4*B+23)/10+C/4-3*(C/100+1)/4
540 Y=MOD(X,7):RETURN
560 REM SUBR. PRINT TABLE
570 Z=1:IF R-J=1 GOTO 610
580 IF S>0 H=0: GOTO 600
590 H=-S*4+4
600 PR$(TOP+H);:Z=Z+1
610 H=R*4
620 PR$(TOP+H);:Z=Z+1:R=R+7
630 IF R<D GOTO 610
640 IF Z>6 PR$(TOP+128);:RETURN
650 H=0:GOTO 620
660 REM SUBR. PRINT DAY OF THE WEEK
670 IF J=0 PR "SU ";:RETURN
680 IF J=1 PR "MO ";:RETURN
690 IF J=2 PR "TU ";:RETURN
700 IF J=3 PR "WE ";:RETURN
710 IF J=4 PR "TH ";:RETURN
720 IF J=5 PR "FR ";:RETURN
730 IF J=6 PR "SA ";:RETURN
740 REM SUBT. PRINT MONTH
750 IF K=1 PR"        JANUARY            FEBRUARY             MARCH"
760 IF K=4 PR"         APRIL               MAY                JUNE"
770 IF K=7 PR"         JULY              AUGUST            SEPTEMBER"
780 IF K=10 PR"       OCTOBER            NOVEMBER           DECEMBER"
790 RETURN
800 REM TAB ASCII-CODE
810 C=#20:D=#0D:E=TOP
820 FOR I=0 TO 3
830 A=#30:A=A+I
840 IF I=0 A=#20
860 FOR J=0 TO 9
870 B=#30:B=B+J
880 @E=A:@(E+1)=B:@(E+2)=C:@(E+3)=D:E=E+4
890 NEXT J
900 NEXT I
910 @TOP=#20:@(TOP+1)=#20:@(TOP+128)=#20:@(TOP+129)=#0D
920 GOTO 10

